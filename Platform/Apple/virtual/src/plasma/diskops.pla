///////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (C) 2015 The 8-Bit Bunch. Licensed under the Apache License, Version 1.1
// (the "License"); you may not use this file except in compliance with the License.
// You may obtain a copy of the License at <http://www.apache.org/licenses/LICENSE-1.1>.
// Unless required by applicable law or agreed to in writing, software distributed under
// the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF
// ANY KIND, either express or implied. See the License for the specific language
// governing permissions and limitations under the License.
///////////////////////////////////////////////////////////////////////////////////////////////////

include "gamelib.plh"
include "globalDefs.plh"
include "playtype.plh"
include "diskops.plh"
include "party.plh"
include "sndseq.plh"
include "gen_images.plh"
include "gen_modules.plh"
include "gen_players.plh"

const RWTS_SEEK  = 0
const RWTS_READ  = 1
const RWTS_WRITE = 2

const RWTS_DRV1  = 0
const RWTS_DRV2  = $80

const RWTS_RDWRPART = (0<<8)
const RWTS_OPENDIR  = (1<<8)

const LOAD_SAVE_BUF = $4E00

const fontEngine    = $EC00   // main mem LC
const fontEngineLen = $F00    // really a bit less, but this leaves space for debug code
const fontData      = $FB00   // main mem LC
const fontDataLen   = $4FA    // really only $474, but we need to fill all gaps

const expandVec     = $200    // aux mem (only for raycaster)
const expandMax     = $3600   // max size of unsplit expander

// Exported functions go here. First a predef for each one, then a table with function pointers
// in the same order as the constants are defined in the the header.
predef _startup()#1
predef _saveGame()#1
predef _loadGame()#1
predef _newOrLoadGame(ask)#1
//AUTOMAP_CHECK// predef _checkAutomap()#1
word[] funcTbl = @_startup, @_saveGame, @_loadGame, @_newOrLoadGame
//AUTOMAP_CHECK// word           = @_checkAutomap

byte[] game1_filename = "GAME.1.SAVE"
byte[] legendos_filename = "LEGENDOS.SYSTEM"

// For checking automap mark sizes
include "gen_mapsizes.pla"

///////////////////////////////////////////////////////////////////////////////////////////////////
// Definitions used by assembly code
asm __defs

; Use hi-bit ASCII for Apple II
!convtab "../../include/hiBitAscii.ct"

; Headers
!source "../../include/global.i"
!source "../../include/plasma.i"
!source "../../include/mem.i"
!source "../../include/fontEngine.i"
!source "../../include/prorwts.i"

; Optional debug printing support
DEBUG   = 0

; General use
tmp         = $2
pTmp        = $4

; Yes, needs to be adjusted 3 places.
expandVec   = $200

end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Set up the font engine
asm setFont(pFont)#0
  +asmPlasmNoRet 1
  jmp SetFont
end

///////////////////////////////////////////////////////////////////////////////////////////////////
asm splitExpander()#1   // returns: remaining lo-aux size
!zone {
  +asmPlasmRet 0
.jsr
  jsr fixedRTS
  tsx
  dex
  lda $100,x
  clc
  adc #.splitexp-.jsr-2
  sta pTmp
  lda $101,x
  adc #0
  sta pTmp+1
  ldy #14
- lda (pTmp),y
  sta $10,y
  dey
  bpl -
  jmp $10
.splitexp
!pseudopc $10 {
  sei     ; prevent interrupts while in aux mem
  sta setAuxRd
  jsr +
  sta clrAuxRd
  cli
  rts
+ jmp (expandVec)
} ; end of pseudopc
} ; end of zone
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Relocate the automap mark queue/flush code to aux LC
asm moveMarks(pMarks)#0
  +asmPlasmNoRet 1
  sta setAuxZP
  bit setLcRW+lcBank1
  bit setLcRW+lcBank1
  sta pTmp
  sty pTmp+1
  ldy #0
- lda (pTmp),y
  sta $DB00,y
  iny
  bne -
  inc pTmp+1
- lda (pTmp),y
  sta $DC00,y     ; two pages of code should be plenty
  iny
  bne -
  bit setLcRW+lcBank2
  sta clrAuxZP
  rts
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Clear the automap mark bits
asm clearMarkBits(pMarks)#0
  +asmPlasmNoRet 1
  sty pTmp+1
.outer
  sta pTmp
  ldy #0
  lda (pTmp),y  ; get map number
  beq .done     ; if zero, end of maps has been reached
  iny
  lda (pTmp),y  ; map len
  tax
  dex
  dex           ; don't count header in length to clear
  iny
  lda #0
- sta (pTmp),y
  iny
  dex
  bne -
  tya           ; cute way to know total length
  clc
  adc pTmp
  bcc .outer
  inc pTmp+1
  bne .outer    ; always taken
.done
  rts
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Params: cmd | open<<8, filename, addr, size. Returns: status (for open only)
asm callProRWTS(cmdPlusOpenFlg, filename, addr, size)#1
    +asmPlasmRet 4
    ; Params come to us from PLASMA in reverse order
    sta setAuxZP
    sta sizelo
    sty sizehi

    sta clrAuxZP
    lda evalStkL+1,x
    ldy evalStkH+1,x
    sta setAuxZP
    sta ldrlo
    sty ldrhi

    sta clrAuxZP
    lda evalStkL+2,x
    ldy evalStkH+2,x
    sta setAuxZP
    sta namlo
    sty namhi

    sta clrAuxZP
    lda evalStkL+3,x
    ldy evalStkH+3,x
    sta setAuxZP
    sta reqcmd
    lda #0
    sta auxreq

    bit setLcRW+lcBank1
    bit setLcRW+lcBank1
    tya
    bne +
    jsr proRWTS
    clc
    bcc ++
+   jsr proRWTS+3
++  lda status
    ldy #0
    bit setLcRW+lcBank2
    bit setLcRW+lcBank2
    sta clrAuxZP
    rts
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// It's a little tricky reseeking ProRWTS's file marker
asm reseekRwts#0
    +asmPlasmNoRet 0
    sta setAuxZP
    lda #0
    ldx #3
-   sta rwts_mark,x
    dex
    bpl -
    sta clrAuxZP
    rts
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Load and display the title screen, set up everything in memory
def _startup()#1
  word pEngine, pFont, pMarks, pExpand, expanderSize

  puts("Loading game.\n")

  auxMmgr(FREE_MEMORY, $800)  // was temporarily reserved by gameloop to keep diskops bytecode out

  // Allocate and permanently lock mem for the font engine and its font (up in LC ram)
  mmgr(START_LOAD, 1) // partition 1 is where code lives
  mmgr(SET_MEM_TARGET, fontEngine)
  mmgr(REQUEST_MEMORY, fontEngineLen)
  mmgr(LOCK_MEMORY,    fontEngine)

  mmgr(SET_MEM_TARGET, fontData)
  mmgr(REQUEST_MEMORY, fontDataLen)
  mmgr(LOCK_MEMORY,    fontData)

  // Load them into lo mem
  pEngine = mmgr(QUEUE_LOAD, CODE_FONT_ENGINE<<8 | RES_TYPE_CODE)
  pFont   = mmgr(QUEUE_LOAD, 1<<8 | RES_TYPE_FONT)
  mmgr(FINISH_LOAD, 0)

  // Relocate font engine and font data to their final spots up in the language card
  memcpy(pEngine, fontEngine, fontEngineLen, 0)
  memcpy(pFont, fontData, fontDataLen, 0)

  // Tell the font engine where to find its font
  setFont(fontData)

  // Load the title screen and show it.
  loadFrameImg(1) // title screen is fixed at #1
  ^$C050     // graphics
  ^$C057     // hi-res
  ^$C054     // page 1
  ^$C052     // full screen
  // Hack for real (not emulated) IIc: sometimes displays only lo-bit graphics
  // unless we do this. *HUGE* thanks to Brendan Robert for the fix!
  ^$C07E=0   // disable double-hi-res
  ^$C05F     // disable double-hi-res
  ^EMUSIG_FULL_COLOR

  // While we're loading, let's get the expander into main RAM (we'll copy to aux later)
  pExpand = mmgr(QUEUE_LOAD, CODE_EXPAND<<8 | RES_TYPE_CODE)

  // Also the automap marking queue/flush code.
  pMarks = mmgr(QUEUE_LOAD, CODE_MARKS<<8 | RES_TYPE_CODE)

  // And the global funcs we'll need for the title screen
  mmgr(QUEUE_LOAD, GS_DISK_LIMIT<<8 | RES_TYPE_MODULE)
  mmgr(QUEUE_LOAD, GS_COPYRIGHT<<8 | RES_TYPE_MODULE)
  mmgr(FINISH_LOAD, 0)

  // Move the marks code up to its out-of-the-way corner of RAM
  moveMarks(pMarks)

  // And free up the font low mem and marks
  mmgr(FREE_MEMORY, pEngine)
  mmgr(FREE_MEMORY, pFont)
  mmgr(FREE_MEMORY, pMarks)

  // Move the expander to aux low mem (which can't be r/w by ProRWTS), then split it
  // (relocating most of it to aux LC ram)
  memcpy(pExpand, expandVec, expandMax, 1) // 1=target qux
  expanderSize = splitExpander()

  // Done with the main mem copy of the expander
  mmgr(FREE_MEMORY, pExpand)

  // To reduce fragmentation, load the resource index in the lowest possible aux mem spot.
  pResourceIndex = $800
  auxMmgr(SET_MEM_TARGET, pResourceIndex)
  auxMmgr(QUEUE_LOAD, CODE_RESOURCE_INDEX<<8 | RES_TYPE_CODE)
  auxMmgr(LOCK_MEMORY, pResourceIndex)
  mmgr(FINISH_LOAD, 0)

  // Record the disk limit
  diskLimit = callGlobalFunc(GS_DISK_LIMIT, 0, 0, 0)

  // All done.
  return 0
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def _rwGame(cmd)#0
  while TRUE
    if callProRWTS(cmd | RWTS_OPENDIR, @game1_filename, LOAD_SAVE_BUF, HEAP_SIZE) == 0
      break
    fin
    textHome()
    ^$c051
    puts("Insert disk 1")
    rdkey()
    ^$c050
  loop
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def readDiskMarks()#1
  word size
  // First, read in the marks. Seek past the heap, then read the length
  if callProRWTS(RWTS_SEEK | RWTS_OPENDIR, @game1_filename, NULL, HEAP_SIZE) <> 0
    fatal("read marks")
  fin
  callProRWTS(RWTS_READ | RWTS_RDWRPART, NULL, LOAD_SAVE_BUF, 2)
  size = *LOAD_SAVE_BUF

  // Now that we have the length, read the rest of the mark data
  callProRWTS(RWTS_READ | RWTS_RDWRPART, NULL, LOAD_SAVE_BUF+2, size)
  return size
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Validate the size of each map field in the automap marks buffer
def checkMarks()#0
  word sp, limit
  byte mapNum, fieldLen, err

  // Make an expanded array for quick access to the map sizes
  memset($200, 0, $100)
  for sp = @mapSizes + 1 to @mapSizes + mapSizes[0] - 1 step 2
    ^($200 + ^sp) = ^(sp+1)
  next

  // Now check the buffer size and the size of each field of map marks
  err = 0
  sp = LOAD_SAVE_BUF+2
  limit = min($6000, sp + *LOAD_SAVE_BUF - 1)
  while TRUE
    if sp > limit; err = 'B'; break; fin
    mapNum = ^sp
    if !mapNum; break; fin
    fieldLen = ^(sp+1)
    if fieldLen <> ^($200 + mapNum); err = 'C'; break; fin
    sp = sp + fieldLen
  loop
  if sp <> limit; err = 'D'; fin
  if err; printf1("code=%c\n", err); fatal("corrupt automap"); fin

  // Print out the heap fullness, for later debugging
  printf1("Heap %d%%\n", curHeapPct)
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// TEMPORARY FOR DISK MARK CHECKING
//AUTOMAP_CHECK// def _checkAutomap()#1
//AUTOMAP_CHECK//   readDiskMarks; checkMarks
//AUTOMAP_CHECK//   return 0
//AUTOMAP_CHECK// end

///////////////////////////////////////////////////////////////////////////////////////////////////
def _saveGame()#1
  // Perform garbage collection and record the size of the heap so we can restore it correctly
  // (also does a CHECK_MEM to be sure we never save corrupted heap)
  heapCollect()

  //AUTOMAP_CHECK// readDiskMarks; checkMarks

  // Copy data to main memory, and write it out.
  memcpy(HEAP_BOTTOM, LOAD_SAVE_BUF, HEAP_SIZE, 0)  // LC to low mem
  _rwGame(RWTS_WRITE)
  return 0
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def loadInternal()#1
  word p_loaded

  // Load data to main memory
  _rwGame(RWTS_READ)

  // Copy the heap up, and init it with the correct size.
  p_loaded = LOAD_SAVE_BUF
  if p_loaded=>w_heapSize == 0
    return FALSE  // no game saved yet
  elsif p_loaded=>w_heapSize < 100 or p_loaded=>w_heapSize > HEAP_SIZE or p_loaded=>w_typeHash <> typeHash
    fatal("Incompatible game file.")
  fin
  memcpy(LOAD_SAVE_BUF, HEAP_BOTTOM, HEAP_SIZE, 0)  // low mem to LC
  initHeap(p_loaded=>w_heapSize)
  heapCollect() // make sure heap is valid, and record final size

  // Check the map marks for valid field sizes
  readDiskMarks; checkMarks

  return TRUE
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def _loadGame()#1
  useMapWindow()
  showMapName("Loading game...")

  // Open the file
  ^$4000 = 0  // so 3D engine knows we overwrite HGR page 2, even if we fail
  if !loadInternal()
    showMapName("Not found.")
    getUpperKey()
  fin
  return 0
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def gameExists()#1
  word p_loaded

  // Load first part of save game into mem... 1 block should be plenty to verify it's real.
  if callProRWTS(RWTS_READ | RWTS_OPENDIR, @game1_filename, LOAD_SAVE_BUF, 512) == 0
    // If heap size is reasonable and type hash matches, chances are high that it's a real save game.
    p_loaded = LOAD_SAVE_BUF
    if p_loaded=>w_heapSize >= 100 and p_loaded=>w_heapSize <= HEAP_SIZE and p_loaded=>w_typeHash == typeHash
      return TRUE
    fin
  fin
  return FALSE
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def getTextKey()#1
  byte key
  ^$c053
  key = getUpperKey()
  ^$c052
  textHome()
  ^$25 = 20
  return key
end

///////////////////////////////////////////////////////////////////////////////////////////////////
//def soundKey()#1
//  byte key
//  word engine, funcTbl
//  key = getTextKey
//  if key <> 'S'; return key; fin
//  mmgr(START_LOAD, 1) // code is in partition 1
//  engine = mmgr(QUEUE_LOAD, MOD_SNDSEQ<<8 | RES_TYPE_MODULE)
//  mmgr(FINISH_LOAD, 0)
//  funcTbl = engine()
//  ^$c053
//  key = funcTbl=>sndseq_play(1) // temporarily, only 1 song
//  ^$c052
//  textHome()
//  ^$25 = 20
//  return key
//end

///////////////////////////////////////////////////////////////////////////////////////////////////
def pressAnyKey()#0
  puts("\n    and press any key to continue.")
  getTextKey()
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def reinsert()#0
  while TRUE
    puts(" Re-insert disk 1")
    pressAnyKey()
    if callProRWTS(RWTS_READ | RWTS_OPENDIR, @legendos_filename, LOAD_SAVE_BUF, 512) == 0
      break
    fin
    puts("\n   ")
    beep()
  loop
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def importGame()#1
  textHome
  ^$c053
  ^$25 = 20
  puts("\n    Insert disk for import")
  pressAnyKey()
  if gameExists()
    loadInternal()
    ^$25 = 20
    puts("\n    Game imported.")
    reinsert()
    _saveGame()
    return TRUE
  fin
  puts("\n    Not found.")
  reinsert()
  return FALSE
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def isNameChar(ch)
  when ch
    is '.'
    is ','
    is '-'
    is '\''
    is ' '
      return TRUE
  wend
  if ch >= 'A' and ch <= 'Z'; return TRUE; fin
  if ch >= '0' and ch <= '9'; return TRUE; fin
  return FALSE
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def isValidName(name)
  byte len, c
  len = ^name
  if !len; return FALSE; fin
  if ^(name+1) == ' '; return FALSE; fin // don't allow space at start
  while len > 0
    len--
    name++
    c = charToUpper(^name)
    if c == ' ' and len == 0; return FALSE; fin // don't allow space at end
    if !isNameChar(c); return FALSE; fin
  loop
  return TRUE
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def getCharacterName()#0
  word cursX, cursY
  while TRUE
    displayStr("Character name?\n")
    cursX, cursY = getCursor()
    setWindow(cursY+24, cursY+24+18, cursX+154, cursX+154+62)
    clearWindow()
    global=>p_players=>s_name = getStringResponse()
    setWindow2()
    setCursor(cursX, cursY)
    if isValidName(global=>p_players=>s_name); break; fin
    displayStr("\nInvalid name.\n\n")
    beep()
  loop
end

def getCharacterGender()#0
  byte gender
  repeat
    displayStr("\n\nGender? (M/F/N/...) \n")
    gender = getUpperKey()
    displayChar(gender)
  until gender >= 'A' and gender <= 'Z'
  global=>p_players->c_gender = gender
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def clearDiskMarks()#0
  word size

  // First, read in the marks.
  size = readDiskMarks()
  if callProRWTS(RWTS_SEEK | RWTS_OPENDIR, @game1_filename, NULL, HEAP_SIZE) <> 0
    fatal("read marks")
  fin
  callProRWTS(RWTS_READ | RWTS_RDWRPART, NULL, LOAD_SAVE_BUF, 2)
  size = *LOAD_SAVE_BUF
  callProRWTS(RWTS_READ | RWTS_RDWRPART, NULL, LOAD_SAVE_BUF+2, size)

  // Clear the fiddly bits
  clearMarkBits(LOAD_SAVE_BUF+2)

  // And write them back
  reseekRwts
  callProRWTS(RWTS_SEEK | RWTS_RDWRPART, NULL, NULL, HEAP_SIZE)
  callProRWTS(RWTS_WRITE | RWTS_RDWRPART, NULL, LOAD_SAVE_BUF, size+2)

  //AUTOMAP_CHECK// _checkAutomap
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def newGame()#0
  word playersModule, newGameModule, partyModule
  clearDiskMarks()
  initHeap(0) // initially empty heap
  global->b_curAvatar = BASE_AVATAR
  global=>w_combatPauseCt = DEFAULT_COMBAT_PAUSE_CT
  global->b_hour = 12  // start at high noon
  global->b_moveMode = 1 // normal
  mmgr(START_LOAD, 1) // players module and new game module both in partition 1
  playersModule = mmgr(QUEUE_LOAD, MOD_GEN_PLAYERS<<8 | RES_TYPE_MODULE)
  newGameModule = mmgr(QUEUE_LOAD, GS_NEW_GAME<<8 | RES_TYPE_MODULE)
  partyModule = mmgr(QUEUE_LOAD, MOD_PARTY<<8 | RES_TYPE_MODULE)
  mmgr(FINISH_LOAD, 0)
  playersModule()=>makeInitialParty()
  loadMainFrameImg()
  ^EMUSIG_2D_MAP
  showMapName("New game")
  setWindow2()
  newGameModule()()
  clearWindow()
  getCharacterName()
  getCharacterGender()
  if global=>p_players->b_skillPoints
    partyModule()=>party_showPlayerSheet(0)
  fin
  heapCollect()
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Print version, right-justified in graphics mode and in a protected text area
def printVersion(windowWidth)#0
  word p
  byte len

  buildString()
  if recordMode
    puts("recording")
  else
    puts(diskLimit ?? "Demo" :: "Full")
    puts(" Version ")
    p = pResourceIndex
    len = readAuxByte(p)
    while len
      p++; printChar(readAuxByte(p)); len--
    loop
  fin
  p = finishString(FALSE)

  // And print in graphics mode too
  if windowWidth
    centerStr(p, windowWidth)
  else
      // Save text cursor, and print at bottom of text window
    ^$23 = 24 // full height window
    ^$25 = 22
    crout()
    ^$24 = 39 - ^p // Right-justify on text screen
    puts(p)
  fin
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def displayMenu(existing)#0
  word p

  setWindow(162, 190, 49, 231)
  clearWindow

  setWindow(162, 190, 56, 224)
  clearWindow()
  rawDisplayStr("^J\n^Y") // down one line for nice-looking spacing from graphics
  callGlobalFunc(GS_COPYRIGHT, 0, 0 ,0)
  rawDisplayStr("\n^N")
  printVersion(168) // win width

  // Prompt the user for the main game options
  rawDisplayStr("^V001")
  buildString()
  puts("Game: N)ew, ")
  if existing; puts("L)oad, "); fin
  puts("I)mport?")
  p = finishString(FALSE)
  centerStr(p, 160)
  puts(p) // display on text screen too, since Jace looks for Import prompt there.
  crout
end

///////////////////////////////////////////////////////////////////////////////////////////////////
def _newOrLoadGame(ask)#1
  byte key, existing
  word ret

  existing = gameExists()
  if existing and !ask
    loadInternal(); return 0
  fin

  // Pause a bit so the player can see the full title screen in all its glory
  pause(2000)

  ^$23 = 24 // Full text window until we're done
  displayMenu(existing)

  ret = -1
  while ret < 0

    // Get their choice and act on it
    key = getUpperKey()
    when key
      is 'N'
        clearWindow
        rawDisplayStr("\n^YStarting new game...^N")
        newGame()
        ret = 1
        break
      is 'L'
        if gameExists
          clearWindow
          rawDisplayStr("\n^YLoading...^N")
          if loadInternal(); ret = 0; break; fin
        else
          beep
        fin
        break
      is 'I'
        clearWindow
        if importGame()
          ret = 0
        else
          displayMenu(existing)
        fin
        break
      is 18 // ctrl-R
        setRecordMode(1)
        break
    wend
  loop

  // Brand future text screens with the version
  printVersion(0) // not on graphics
  ^$23 = 23 // adjust window to protect it
  textHome

  // All done
  return ret
end

///////////////////////////////////////////////////////////////////////////////////////////////////
// Boilerplate module initialization code
return @funcTbl
done
